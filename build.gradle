plugins {
  id 'java'
  id 'cpp'
  id 'visual-studio'
  id 'eclipse'
}

model {
  repositories {
    libs(PrebuiltLibraries) {
      jni.headers.srcDirs "${System.env.JAVA_HOME}/include", "${System.env.JAVA_HOME}/include/win32"
      dokan {
        headers.srcDir System.env.DOKAN_HOME
        binaries.withType(SharedLibraryBinary) {
          sharedLibraryLinkFile = file("${System.env.DOKAN_HOME}/dokan.lib")
	}
      }
    }
  }
  components {     
    'native'(NativeLibrarySpec) {
      baseName 'jdokan'
      binaries.all {
        cppCompiler.define "UNICODE"
      }
      sources.cpp {
        lib library: 'jni', linkage: 'api'
        lib library: 'dokan', linkage: 'shared'
        source {
          srcDir "src/main/cpp"
          include "**/*.cpp"
        }
        exportedHeaders {
          srcDir "src/main/cpp/include"
        }
      }
    }
  }
}

tasks.matching{ it.name.contains('StaticLibrary') }
  .whenTaskAdded { it.enabled = false }
tasks.matching{ it.name.contains('LibVisualStudio') }
  .whenTaskAdded { it.enabled = false }


repositories {
  jcenter()
}

version = '1.1-SNAPSHOT'

dependencies {
  compile 'com.google.guava:guava:18.+'
  testCompile 'junit:junit:4.+'
  testCompile 'com.google.truth:truth:0.25'
}

eclipse {
  project.natures 'org.springsource.ide.eclipse.gradle.core.nature'
}

task generateHeader {
  dependsOn('classes')
  def clazz = 'net.decasdev.dokan.Dokan'
  def header = "${rootDir}/src/main/cpp/include/jdokan_jni.h"
  inputs.file "${sourceSets.main.output.classesDir}/${clazz.replaceAll('.', '/')}"
  outputs.file header
  doLast {
    ant.javah(class: clazz, outputFile: header, classpath: sourceSets.main.output.classesDir)
  }
}
